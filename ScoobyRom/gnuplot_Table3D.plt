# gnuplot TEMPLATE for Table3D

# command "test" is useful to check terminal specific colors, linetypes, capabilities...
# test

# -------- data specific commands generated by the app, before loading this template file:
# set xlabel "Engine Speed\n{/mono [1/min]}" ...
# set ylabel ...
# set title ...
# load "this_template_file.plt"
# --------

# TERMINAL SPECIFIC !!! Must match currently used terminal!!!! Here: wxt
# set term wxt font "Liberation Sans,14"
# use default (platform specific) sans-serif font, font size 14
set term wxt font "sans,14"

# to allow inline text formatting. If fontname given it must follow '/'.
set termoption enhanced

# HSV color interpolation: min, max as HSV
set palette model HSV defined (0 0.66666667 0.4 1, 1 0 0.6 1)

# default: front = in front of actual plot
set colorbox back

# horizontal, wide and near bottom if wanted
# set colorbox horiz user origin .1,.1 size .8,.04

# define style id 100 for surface lines (polygon frame), linetype -1 always means solid black line
set style line 100 lt -1 lc "black" linewidth 0.5

# coloring lines not working with pm3d ?? not really needed here anyway
# set style line 100 palette

# also draw lines, use own style 100
set pm3d hidden3d 100

# add contour lines; displayed on base, surface or both
set contour surface
# changing this needs replot:
# bspline may not be accurate but is always smooth
set cntrparam bspline
set cntrparam levels auto 10

# default is like set clabel: multiple contour colors plus legend
# however some colors don't separete well from surface fill
#set clabel

# one color only and no contour legend
unset clabel

# page 94: specifying contour linetypes etc. not possible?
# "gnuplot will vary the linetype used for each contour level when clabel is set."
# more contour format control requires writing contours into file and plot from there...


# p.156: "By default splot draws the xy plane completely below the plotted data. The oﬀset between the lowest ztic and the xy plane can be changed by set xyplane."
# p.151: xyplane; default: set xyplane relative 0.5
# set xyplane at 20
# should work for most/all 3d plots, little space so that lowest z-label does not collide with xy-label
set xyplane relative 0.05

# clipping effect or to adjust lowest value to base surface etc.
# set zrange [20:62]
# autoscale:
# set zrange [*:*]


# show default grid
set grid

# set view <rot_x>{,{<rot_z>}{,{<scale>}{,<scale_z>}}}
# defaults to: set view 60, 30, 1, 1
# <0 not allowed: rot_x [0:180], rot_z [0:360]
set view 30,330

# add useful key binding: press "home", sometimes labeled "pos1", key to get initial viewpoint
bind Home "set view 30,330; refresh"

# without specifying title, "<filename> binary" would appear above contour legend!
# empty title avoids this
# app generates binary data file "gnuplotBinary.bin" for each plot, basically a temporary file
# mark as "volatile" so a single temporary data file can be shared, replot would re-read and could show wrong data
splot "gnuplot_data.tmp" binary volatile title "" with pm3d

#set label 1 "Annotation Label" at screen 0.01,0.95 front left textcolor rgb "blue"


# -------- SVG output (commands sent via standard input from app)
#ä could also do PDF, LaTeX, PNG etc.
## remember current terminal
# set terminal push
# set terminal svg size 1200,800 dynamic enhanced font "sans,16"
# set output "SomeFileName.svg"
# refresh
## switch back to previous terminal
# set terminal pop
# --------
